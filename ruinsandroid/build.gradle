apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'com.github.dcendents.android-maven'

group = 'com.github.ruinszero'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'


    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 2
        versionName "1.0.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    testImplementation 'junit:junit:4.12'
    //依赖注入
    implementation butterknife
    kapt butterknife_compiler
    implementation dagger2
    kapt dagger2_compiler
    implementation dagger2_android
    implementation dagger2_android_support
    kapt dagger2_android_compiler
    //权限相关
    implementation rxpermissions
    //rxjava
    implementation rxjava2
    implementation rxjava2_android
    //json解析库
    implementation gson
    //支持库
    implementation design
    implementation support
    implementation support_v7
    implementation cardview
    implementation recyclerview
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    //圆角头像
    implementation circle
    //网络库
    implementation retrofit2
    implementation retrofit2_rxjava2
    implementation retrofit2_gson
    implementation okhttp_log
    //RecyclerView BaseViewHolder
    implementation baseAdapter
    //lifecycles
    implementation lifecycles_extensions
    implementation lifecycles_reactivestreams
    //logger
    implementation log
    //rxbinding
    implementation rxbinding
    implementation rxbinding_support_v4
    implementation rxbinding_appcompat_v7
    //状态栏
    implementation statusbarutil
}

// 指定编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// 打包源码jar
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// 制作文档(Javadoc)
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
